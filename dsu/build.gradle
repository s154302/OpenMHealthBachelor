buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    ext {
        springBootVersion = "1.3.3.RELEASE"
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    group = 'org.openmhealth'
    version = '0.1.0'

    repositories {
        mavenLocal()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'spring-boot' // used for auto-configuration

    ext {
        javaVersion = 1.8
        omhSchemaSdkVersion = '1.0.5'
        mailApiVersion = '1.5.2'
    }

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    dependencies {
        compile 'org.hibernate:hibernate-validator'
        compile "com.fasterxml.jackson.core:jackson-core"
        compile "com.fasterxml.jackson.core:jackson-databind"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        compile "com.sun.mail:mailapi:${mailApiVersion}" // TODO confirm if both are necessary
        compile "javax.mail:javax.mail-api:${mailApiVersion}" // required to support InternetAddress fields
        compile "org.openmhealth.schema:omh-schema-sdk:${omhSchemaSdkVersion}"
        compile 'org.slf4j:slf4j-api'
        compile 'org.springframework:spring-beans'
        compile "org.springframework.boot:spring-boot-autoconfigure"
        compile 'org.springframework:spring-context'
        compile "org.springframework.security.oauth:spring-security-oauth2"
        compile 'javax.validation:validation-api'

        testCompile 'org.hamcrest:hamcrest-library'
        testCompile 'junit:junit'
        testCompile 'org.mockito:mockito-core'
        testCompile 'org.springframework:spring-test'

        runtime 'org.slf4j:jcl-over-slf4j'
        runtime 'org.slf4j:log4j-over-slf4j'
        runtime 'ch.qos.logback:logback-classic'
        runtime "org.yaml:snakeyaml"
    }
}

configure(project(":shared")) {

    // disable trying to make this project's JAR file executable
    bootRepackage {
        enabled = false
    }
}

configure([
        project(":authorization-server"),
        project(":resource-server")]) {

    bootRepackage {
        mainClass = "org.openmhealth.dsu.configuration.Application"
    }

    dependencies {
        compile project(':shared')
        compile 'org.apache.httpcomponents:httpclient'
        compile "org.springframework.boot:spring-boot-starter-jetty"
        compile("org.springframework.boot:spring-boot-starter-web") {
            exclude module: "spring-boot-starter-tomcat"
        }
        compile 'org.springframework:spring-web'
        compile 'org.springframework:spring-webmvc'

        // you can comment out any dependencies you know you don't need after this point
        compile 'org.mongodb:mongo-java-driver'
        // compile 'mysql:mysql-connector-java'
        compile "org.postgresql:postgresql"
    }

    task copyArchiveJarToDockerContext(dependsOn: assemble, type: Copy) {
        from 'build/libs'
        into 'docker'
        include "${jar.archiveName}"
        rename { String fileName ->
            fileName.replace("${jar.archiveName}", "${jar.baseName}.jar")
        }
    }
    build.dependsOn copyArchiveJarToDockerContext
}

// FIXME add integration test support

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
